import org.apache.tools.ant.filters.ReplaceTokens

import java.nio.charset.StandardCharsets

plugins {
    id 'idea'
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'io.github.honhimw'
//version = '1.6.0.1-SNAPSHOT' // API docs version plus '.x(-SNAPSHOT)?'
version = '1.6.0.1' // API docs version plus '.x(-SNAPSHOT)?'
description = 'Reactive meilisearch rest client power by reactor-netty-http.'

def title = 'Meilisearch rest client for java'
def reactorVersion = '2023.0.1'
def jacksonVersion = '2.15.3'
def gsonVersion = '2.10.1'
def yassonVersion = '2.0.4'
def lombokVersion = '1.18.26'
def snapshots = version.toString().endsWith('SNAPSHOT')

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation platform("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
    implementation platform("io.projectreactor:reactor-bom:${reactorVersion}")

    implementation "io.projectreactor.netty:reactor-netty-core"
    implementation "io.projectreactor.netty:reactor-netty-http"

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    compileOnly "com.google.code.gson:gson:${gsonVersion}"

    compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    compileOnly 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.19'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation "com.google.code.gson:gson:${gsonVersion}"
//    testImplementation 'com.meilisearch.sdk:meilisearch-java:0.11.5'
    testCompileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.slf4j:slf4j-simple:2.0.9'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

processTestResources {
    def nm = new LinkedHashMap<String, String>()
    project.properties.forEach {k, v ->
        nm.put(k, v.toString())
    }
    nm.put('project.artifactId', project.name)
    def properties = loadProfileProperties()
    properties.forEach { k, v ->
        nm.put(String.valueOf(k), String.valueOf(v))
    }
    filter ReplaceTokens, beginToken: '#{', endToken: '}', tokens: nm
}

publishing {

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/honhimW/meilisearch-rest-client")
            credentials {
                username = githubProperties().getProperty("gpr.user") as String
                password = githubProperties().getProperty("gpr.key") as String
            }
        }
        maven {
            name = "sonatype"
            if (snapshots) {
                url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            } else {
//                url = uri("https://s01.oss.sonatype.org/content/repositories/releases/")
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            }
            credentials {
                username = sonatypeProperties().getProperty("sonatype.username") as String
                password = sonatypeProperties().getProperty("sonatype.password") as String
            }
        }
    }

    publications {
        create('gpr', MavenPublication) {
            from components.java
            pom {
                name.set(title)
                description.set(project.description)
                url.set("https://github.com/honhimW/meilisearch-rest-client")
                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('https://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }
                developers {
                    developer {
                        name.set('honhimw')
                        email.set('honhimw@outlook.com')
                        url.set('https://honhimW.github.io')
                    }
                }
            }
        }
    }

}

jar {
    enabled(true)
    archiveClassifier = ''
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = StandardCharsets.UTF_8.name()
    inputs.files(tasks.withType(ProcessResources))

}

test {
    useJUnitPlatform()
}

Properties loadProfileProperties() {
    def properties = new Properties()

    def file = file("profile-${findProperty('profiles.active')}.properties")
    if (file.exists()) {
        file.withInputStream { properties.load(it) }
    }
    return properties
}

Properties githubProperties() {
    def properties = new Properties()

    def file = file("github.properties")
    if (file.exists()) {
        file.withInputStream { properties.load(it) }
    }
    return properties
}

Properties sonatypeProperties() {
    def properties = new Properties()

    def file = file("sonatype.properties")
    if (file.exists()) {
        file.withInputStream { properties.load(it) }
    }
    return properties
}